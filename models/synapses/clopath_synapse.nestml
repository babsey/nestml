"""
References
++++++++++

.. [1] Clopath et al. (2010). Connectivity reflects coding:
       a model of voltage-based STDP with homeostasis.
       Nature Neuroscience 13:3, 344--352. DOI: https://doi.org/10.1038/nn.2479
.. [2] Clopath and Gerstner (2010). Voltage and spike timing interact
       in STDP - a unified model. Frontiers in Synaptic Neuroscience 2:25.
       DOI: https://doi.org/10.3389/fnsyn.2010.00025
.. [3] Voltage-based STDP synapse (Clopath et al. 2010) on ModelDB
       https://senselab.med.yale.edu/ModelDB/showmodel.cshtml?model=144566
"""
model clopath_synapse:
    state:
        w real = 1.    @nest::weight   # Synaptic weight
        pre_trace real = 0.
        post_membrane_potential_avg_plus mV = -70.6 mV
        post_membrane_potential_avg_minus mV = -70.6 mV
        post_membrane_avg_avg mV = -70.6 mV

    parameters:
        d ms = 1 ms  @nest::delay   # Synaptic transmission delay
        w_max real = 100.
        tau_post_membrane_avg_plus ms = 7 ms
        tau_post_membrane_avg_minus ms = 10 ms
        tau_post_membrane_avg_avg ms = 500 ms
        tau_pre_tr ms = 15 ms
        theta_minus mV = -70.6 mV
        theta_plus mV = -45.3 mV   # should be greater than theta_minus
        A_LTD real = 14e-5
        A_LTP real = 8e-5
        u_ref real = 7.74

    equations:
        pre_trace' = -pre_trace / tau_pre_tr
        post_membrane_potential_avg_plus' = (-post_membrane_potential_avg_plus + post_membrane_potential) / tau_post_membrane_avg_plus
        post_membrane_potential_avg_minus' = (-post_membrane_potential_avg_minus + post_membrane_potential) / tau_post_membrane_avg_minus
        post_membrane_avg_avg' = (-post_membrane_avg_avg + post_membrane_potential_avg_minus) / tau_post_membrane_avg_avg

    input:
        pre_spikes <- spike
        post_spikes <- spike
        post_membrane_potential mV <- continuous

    output:
        spike

    onReceive(post_spikes):
        println("Post spike received")
        if post_membrane_potential > theta_plus and post_membrane_potential_avg_plus > theta_minus:
            # potentiate synapse
            println("\tPotentiating synapse, pre_trace = {pre_trace}")#, post_membrane_potential = {post_membrane_potential}") #, post_membrane_potential_avg_plus = {post_membrane_potential_avg_plus}")
            # w += A_LTP * pre_trace * (post_membrane_potential - theta_plus) * (post_membrane_potential_avg_plus) - theta_minus)
            w += A_LTP * pre_trace * (post_membrane_potential - theta_plus) * (post_membrane_potential_avg_plus - theta_minus)
            w = min(w, w_max)

    onReceive(pre_spikes):
        println("Pre spike received")
        pre_trace += 1 / tau_pre_tr

        if post_membrane_potential_avg_minus > theta_minus:
            # depress synapse
            foobar mV = post_membrane_potential_avg_minus
            println("\tDepressing synapse, post_membrane_potential_avg_minus = {foobar}")
            #w -= A_LTD * (post_membrane_potential_avg_minus - theta_minus)
            w -= A_LTD * post_membrane_avg_avg**2 * ( post_membrane_potential_avg_minus - theta_minus ) / u_ref**2
            w = max(w, 0.)

        # deliver spike to postsynaptic partner
        emit_spike(w, d)

    update:
        integrate_odes()
